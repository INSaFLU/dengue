if not config:
    configfile: "config/config_dengue.yaml"

serotypes = ['all', 'denv1', 'denv2', 'denv3', 'denv4']

rule all:
    input:
        auspice_json = expand("auspice/dengue_{serotype}.json", serotype=serotypes)

rule files:
    params:
        dropped_strains = "config/dropped_strains.txt",
        reference = "config/reference_dengue_{serotype}.gb",
        auspice_config = "config/auspice_config_{serotype}.json"

files = rules.files.params

def download_serotype(w):
    serotype = {
        'all': 'all',
        'denv1': 'Dengue_virus_1',
        'denv2': 'Dengue_virus_2',
        'denv3': 'Dengue_virus_3',
        'denv4': 'Dengue_virus_4'
    }
    return serotype[w.serotype]

def filter_sequences_per_group(w):
    sequences_per_group = {
        'all': '10',
        'denv1': '36',
        'denv2': '36',
        'denv3': '36',
        'denv4': '36'
    }
    return sequences_per_group[w.serotype]

def traits_columns(w):
    traits = {
        'all': 'region',
        'denv1': 'country region',
        'denv2': 'country region',
        'denv3': 'country region',
        'denv4': 'country region'
    }
    return traits[w.serotype]

def clade_defs(w):
    defs = {
        'all': 'config/clades_serotypes.tsv',
        'denv1': 'config/clades_genotypes.tsv',
        'denv2': 'config/clades_genotypes.tsv',
        'denv3': 'config/clades_genotypes.tsv',
        'denv4': 'config/clades_genotypes.tsv'
    }
    return defs[w.serotype]

include: "rules/prepare_sequences.smk"
include: "rules/construct_phylogeny.smk"

rule ancestral:
    """Reconstructing ancestral sequences and mutations"""
    input:
        tree = "results/tree_{serotype}.nwk",
        alignment = "results/aligned_{serotype}.fasta"
    output:
        node_data = "results/nt-muts_{serotype}.json"
    params:
        inference = "joint"
    shell:
        """
        augur ancestral \
            --tree {input.tree} \
            --alignment {input.alignment} \
            --output-node-data {output.node_data} \
            --inference {params.inference}
        """

rule translate:
    """Translating amino acid sequences"""
    input:
        tree = "results/tree_{serotype}.nwk",
        node_data = "results/nt-muts_{serotype}.json",
        reference = files.reference
    output:
        node_data = "results/aa-muts_{serotype}.json"
    shell:
        """
        augur translate \
            --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.reference} \
            --output {output.node_data} \
        """

rule traits:
    """
    Inferring ancestral traits for {params.columns!s}
      - increase uncertainty of reconstruction by {params.sampling_bias_correction} to partially account for sampling bias
    """
    input:
        tree = "results/tree_{serotype}.nwk",
        metadata = "data/metadata_{serotype}.tsv"
    output:
        node_data = "results/traits_{serotype}.json",
    params:
        columns = traits_columns,
        sampling_bias_correction = 3,
        strain_id = config.get("strain_id_field", "strain"),
    shell:
        """
        augur traits \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --output {output.node_data} \
            --columns {params.columns} \
            --confidence \
            --sampling-bias-correction {params.sampling_bias_correction}
        """

rule clades:
    """Annotating serotypes / genotypes"""
    input:
        tree = "results/tree_{serotype}.nwk",
        nt_muts = "results/nt-muts_{serotype}.json",
        aa_muts = "results/aa-muts_{serotype}.json",
        clade_defs = clade_defs,
    output:
        clades = "results/clades_{serotype}.json"
    shell:
        """
        augur clades \
            --tree {input.tree} \
            --mutations {input.nt_muts} {input.aa_muts} \
            --clades {input.clade_defs} \
            --output {output.clades}
        """

rule export:
    """Exporting data files for for auspice"""
    input:
        tree = "results/tree_{serotype}.nwk",
        metadata = "data/metadata_{serotype}.tsv",
        branch_lengths = "results/branch-lengths_{serotype}.json",
        traits = "results/traits_{serotype}.json",
        clades = "results/clades_{serotype}.json",
        nt_muts = "results/nt-muts_{serotype}.json",
        aa_muts = "results/aa-muts_{serotype}.json",
        auspice_config = files.auspice_config
    output:
        auspice_json = "results/raw_dengue_{serotype}.json",
        root_sequence = "results/raw_dengue_{serotype}_root-sequence.json",
    params:
        strain_id = config.get("strain_id_field", "strain"),
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --node-data {input.branch_lengths} {input.traits} {input.clades} {input.nt_muts} {input.aa_muts} \
            --auspice-config {input.auspice_config} \
            --include-root-sequence \
            --output {output.auspice_json}
        """

rule final_strain_name:
    input:
        auspice_json="results/raw_dengue_{serotype}.json",
        metadata="data/metadata_{serotype}.tsv",
        root_sequence="results/raw_dengue_{serotype}_root-sequence.json",
    output:
        auspice_json="auspice/dengue_{serotype}.json",
        root_sequence="auspice/dengue_{serotype}_root-sequence.json",
    params:
        strain_id=config.get("strain_id_field", "strain"),
        display_strain_field=config.get("display_strain_field", "strain"),
    shell:
        """
        python3 bin/set_final_strain_name.py \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --input-auspice-json {input.auspice_json} \
            --display-strain-name {params.display_strain_field} \
            --output {output.auspice_json}
        cp {input.root_sequence} {output.root_sequence}
        """

rule clean:
    """Removing directories: {params}"""
    params:
        "results ",
        "auspice"
    shell:
        "rm -rfv {params}"
